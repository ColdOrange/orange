function(orange_define_tests)
    set(arg_index 0)
    while(arg_index LESS ${ARGC})
        if ("x${ARGV${arg_index}}" STREQUAL "xTEST")
            math(EXPR arg_index "${arg_index} + 1")
            if (NOT arg_index LESS ${ARGC})
                message(FATAL_ERROR "Expected test name.")
            endif()
            set(cur_test_name ${ARGV${arg_index}})

            math(EXPR arg_index "${arg_index} + 1")
            if (NOT arg_index LESS ${ARGC} OR NOT "x${ARGV${arg_index}}" STREQUAL "xSOURCES")
                message(FATAL_ERROR "Expected arguement SOURCES for ${cur_test_name}.")
            endif()
            set(cur_test_sources)

            math(EXPR arg_index "${arg_index} + 1")
            while(arg_index LESS ${ARGC})
                if ("x${ARGV${arg_index}}" STREQUAL "xTEST")
                    break()
                else()
                    list(APPEND cur_test_sources ${ARGV${arg_index}})
                    math(EXPR arg_index "${arg_index} + 1")
                endif()
            endwhile()
            if (NOT cur_test_sources)
                message(FATAL_ERROR "Expected at least one source file for ${cur_test_name}.")
            endif()

            add_executable(${cur_test_name} ${cur_test_sources})
            target_include_directories(${cur_test_name}
                PRIVATE ${PROJECT_SOURCE_DIR}/orange
                PRIVATE ${PROJECT_SOURCE_DIR}/third_party/Catch2/single_include/catch2
            )
            add_test(NAME ${cur_test_name} COMMAND ${cur_test_name} -o report.xml -r junit)
        else()
            message(FATAL_ERROR "Expected arguement TEST.")
        endif()
    endwhile()
endfunction()
